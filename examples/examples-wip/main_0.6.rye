
; the "web framework" so far are just these 5 functions

write-err: fn { err w } { write-header w code? err , write w to-json err }
write-ok: fn { d w } { write w to-json d }

webfn: fn { spec args body } {
	fnc args current-context {
		set-content-type w "application/json"
		full-form? r |validate spec
		|to-context |do-in body
	}
}
handle-web-resource: fn { w r res } {
	query? r "_m" |to-word :name
	res -> name :func
	func w r |^fix { .write-err w } |write-ok w
}

handle-web-resources: fn { w r resmap } {
	query? r "_r" |to-word :name
	resmap -> name |first :res
	handle-web-resource w r res
}

;  web-app / api code , two resources each with one method

account-resource: context {
 
	sign-in: webfn { user: required  password: required } { w r } {
		require all { user = "user" password = "pwd123" }
		|^check { 403 "wrong credentials" }
		"ok"
	}

}

page-resource: context {
 
	get-page: webfn { id: required integer } { w r } {
		require id = 77
		|^check { 404 "page doesn't exist" }
		"Seventh page of a seventh page ..."
	}

}

handle-api: fn { w r } {
	handle-web-resources w r context {
		page: eval { page-resource "Hanldes pages" }
		account: eval { account-resource "Handles user accounts" }
	}
}

new-server ":8080"
|handle "/api" ?handle-api
|serve
