
chat-tui: context {

	extend: fn { n c } { produce n "" { .concat c } }
	line: fn1 { .extend "─" }
	
	gradual-print: fn1 { .for { .prn , sleep 30 } }
	
	draw-box: fn { cols rows offset } {
		right offset , "╭" |+ line cols + 3 |+ "╮" |print
		for range 1 rows {
			right offset , "│" |+ extend cols + 3 " " |+ "│" |print
		}
		right offset , either offset > 0 {
			"╰" |+ line cols + 3 |+ "┴" |print
		} {
			"┴" |+ line cols + 3 |+ "╯" |print
		}
	}
	
	chat: fn { t } {
		.length? |draw-box 1 0
		up 2 , right 3 , gradual-print t , down 1 , print ""
	}

	chat-of: fn { t name } {
		.pass { color\yellow }
		|length? |draw-box 1 0
		, print name
		up 3 , right 3 , gradual-print t , down 1 , print "" , print ""
		color\reset
	}

	chat-lines: fn { ts } {
		.map { .length? } |max |draw-box rows: length? ts 0
		up rows + 1
		for ts { .pass { right 3 } |gradual-print sleep 400 print "" }
		print ""
	}

	choice: fn { ch } {
		draw-box 60 2 40 , up 3
		tui\selection ch 42
	}

	input: fn { len } {
		draw-box len + 3 1 60 , up 2
		tui\input 62 len
		|pass { down 1 }
	}
	
	up:    fn1 { .esc-val "[(*)A" |prn }
	down:  fn1 { .esc-val "[(*)B" |prn }
	right: fn1 { .esc-val "[(*)C" |prn }
	left:  fn1 { .esc-val "[(*)D" |prn }
	bold:  does { esc "[1m"  |prn }
	reset: does { esc "[22m" |prn }

	color\black:     does { esc "[30m" |prn }
	color\red:     does { esc "[31m" |prn }
	color\green:     does { esc "[32m" |prn }
	color\yellow:     does { esc "[33m" |prn }
	color\blue:     does { esc "[34m" |prn }
	color\magenta:     does { esc "[35m" |prn }
	color\cyan:     does { esc "[36m" |prn }
	color\white:     does { esc "[37m" |prn }
	color\default: does { esc "[39m" |prn }
	color\reset:   does { esc "[0m"  |prn }
}
