
; Rye example of bleve full-text-search - a simple search console

rye .needs { bleve }

private\ "console will run inside this context" {

	private\ "opens or creates a bleve index and returns it" {
		%demo8.bleve :db |open-bleve
		|fix {
			new-bleve-document-mapping :fqm
			new-bleve-text-field-mapping :tfm
			
			new-bleve-index-mapping :im
			|add-document-mapping "faq" fqm
			|add-field-mapping-at "question" tfm
			|add-field-mapping-at "answer" tfm
			|add-field-mapping-at "keywords" tfm
			
			im .new-bleve db
		}
	} :idx
	; we can index a JSON string
	|index "presale1" ${ "q": "Is InvoiceFox really free?", "a": "Yes, our Mini plans are absolutely free.", "kw": "billing,free,plans" }$
	|index "presale2" ${ "q": "Can I quit at any time?", "a": "Yes, and you can change packages at any time.", "kw": "billing,plans" }$
	|index "integra1" ${ "q": "Can I integrate with Woocommerce? ", "a": "Yes, we have open source WP plugin on github.", "kw": "webshop,integration" }$
	|index "integra2" ${ "q": "Can I integrate inventory and mobile POS?", "a": "Yes, mobile POS can affect and check the inventory.", "kw": "pos,integration" }$

	; define two functions that we will be able to call in console
	search: fn1 { .new-match-query |new-search-request :sr Search sr idx }
	index: fn { id data } { Index idx id data }

	; enter the Rye console
	enter-console "you can index and search here"
	
}

