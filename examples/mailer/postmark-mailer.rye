
rye .needs { postmark mysql }

on-error { .log }

db: open mysql://user@mailer load\first %.mysqlpwd

rule: { _from: required email _to: required email subject: required } 

do-mailer: does {

	query db {
		select * from mailer
		where status < 3
		order by id limit 1 }
	|first |^fix { "no messages" }
	|pass { -> 'id :id , -> 'status :status }
	|send-message
	|fix\else { inc! 'status } { status: 100 }

	exec db { update mailer set status = ?status where id = ?id }
}

construct-message: fn { row } {

	.validate rule |^check. :row
	
	new-postmark-email
	|from<- '_from <- row
	|to<- '_to <- row
	|subject<- 'subject <- row
	|text-body<- 'content <- row :msg

	if f: 'file <- row {
		msg .attach! to-file f |^check { "error attaching: " + f }
	}
	msg
}

send-message: fn { row } {
	
	on-error { ^check { "error sending message: " + 'id <- row } } ,
	
	.construct-message
	|send open-postmark load\first %.token
}

loop 5 { do-mailer , sleep 500 }
