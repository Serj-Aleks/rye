
; Fryework - rye web framework

write-err: fn { err w } { write-header w 403 , write w to-json err }
write-ok: fn { d w } { write w to-json d }

webfn: fn { spec args body } {
	fnc args current-context {
		set-content-type w "application/json"
		full-form? r |validate spec |^check 0
		|to-context |do-in body
	}
}

handle-web-resource: fn { w r res } {
	query? r "_m" |to-word :name
	res -> name |^check { 404 "method not found" } :func
	func w r |^fix { .write-err w } |write-ok w
}

handle-web-resources: fn { w r resmap } {
	query? r "_r" |to-word :name
	resmap -> name |^fix { new-error { 404 "resource not found" } |write-err w } |first :res
	handle-web-resource w r res |^fix { .write-err w }
}

page: fn { title body } { join eval { "<html><title>" title "</title><body>" body "</body></html>" } }
