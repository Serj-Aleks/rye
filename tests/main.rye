; Testing and Documenting frameworks that do test and generate html reference docs
; Run test cases: ../rye main.rye test
; Generate documentation: ../rye main.rye doc > builtins.html

rye .args\raw :arg
root-ctx: current-ctx

test-framework: context {

	cnt: 0
	failed: 0
	
	section: fn { name descr code } { print join { "SECTION: " name newline " * " descr } , do code }
	
	group: fn { name descr args code } { inc! 'cnt print " GROUP: " + name , do code }
	
	equal: fn { test res } {
		do-in root-ctx test :got = res
		|either { print "  - OK" } { inc! 'failed , print join { "  - Error: expected " mold res ", got " mold got } }
	}
	
	stdout: fn { test res } {
		capture-stdout { do-in root-ctx test } :got = res
		|either { print "  - OK" } { inc! 'failed , print join { "  - Error: expected " mold res ", got " mold got } }
	}

	comment: fn { str } {
		
	}

	end: fn { } { print "CNT: " + cnt , print "FAILED: " + failed , if failed > 0 { exit 1 } }

}

docs-framework: context {
	
	section: fn { name descr code } { print join { "<h2>" name "</h2><p>" descr "</p><div class='section'>" } , do code , print "</div>" }
	
	group: fn { name descr args code }   { print join { "<h3>" name "</h3><p>" descr "</p><div class='group'>" } , do code , print "</div>" }

	equal: fn { test res } {
		do-in root-ctx test :got = res
		|if { print join [ "<pre class='prettyprint lang-rye'><code language='lang-rye'>" mold\nowrap test newline "; returns "  replace mold got newline newline + "; "  "</code></pre>" ] }
	}

	stdout: fn { test res } {
		capture-stdout { do-in root-ctx test } :got = res
		1 |if { print join [ "<pre class='prettyprint lang-rye'><code language='lang-rye'>" mold\nowrap test newline "; prints " replace mold\nowrap got newline newline + "; " "</code></pre>" ] }
	}

	comment: fn { str } {
		print join [ "<p class='comment'>" str "</p>" ]
	}

	end: fn { } { }

	
}

generate-menu: fn { menu } {
	.fold 'acc "" { :x acc .concat " &middot; <a href='{{}}.html'>{{}}</a>" .replace "{{}}" x }
}

generate-doc-file: fn { filename menu } {
	join {
		%tpl/header.html .read .replace "{{title}}" capitalize filename |replace "{{menu}}" generate-menu menu
		capture-stdout { do-in docs-framework load to-file filename + ".rye" }
		read %tpl/footer.html
	} |write* to-file filename + ".html"
}

menu: { "basics" "structures" "spreadsheets" }

switch arg {
	"test" { do-in test-framework load %builtins.rye }
	"doc"  {
		generate-doc-file "basics" menu
		generate-doc-file "structures" menu
 		generate-doc-file "validation" menu
		generate-doc-file "misc" menu
	}
	_ { print "use test or doc command" }
} 













;   maybe we need this at some point 
;	true: fn { test } {
;		do-in root-ctx test
;		|either { print "  - OK" } { print "  - Error: not True" }
;	}
